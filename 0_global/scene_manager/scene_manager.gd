extends CanvasLayer

signal load_scene_started
signal new_scene_ready(target_name: String, offset: Vector2i)
signal load_scene_finished

@onready var fade: Control = $Fade

func _ready() -> void:
	fade.visible = false

	await get_tree().process_frame
	load_scene_finished.emit()


func transition_scene( new_scene: String,
		target_area: String,
		player_offset: Vector2i,
		dir: String) -> void:

	load_scene_started.emit()
	get_tree().paused = true

	var fade_pos = get_fade_position(dir)
	fade.visible = true
	await fade_screen(fade_pos, Vector2.ZERO)

	print("attempting to change to " + str(new_scene))
	get_tree().change_scene_to_file.call_deferred(new_scene)
	await get_tree().scene_changed

	new_scene_ready.emit(target_area, player_offset)

	await fade_screen(Vector2.ZERO, -fade_pos)

	get_tree().paused = false
	fade.visible = false
	load_scene_finished.emit()


func fade_screen(from: Vector2, to: Vector2) -> Signal:
	fade.position = from
	var tween = create_tween()
	tween.tween_property(fade, "position", to, 0.5)
	return tween.finished


func get_fade_position(dir: String) -> Vector2:
	var fade_pos := Vector2(GameManager.viewport_size) * 2

	match dir:
		"left":
			fade_pos *= Vector2(-1, 0)
		"right":
			fade_pos *= Vector2(1, 0)
		"up":
			fade_pos *= Vector2(0, -1)
		"down":
			fade_pos *= Vector2(0, 1)

	return fade_pos


func get_player() -> CharacterBody2D:
	var player: CharacterBody2D = null
	while not player:
		if is_inside_tree():
			await get_tree().process_frame
		if is_inside_tree():
			player = get_tree().get_first_node_in_group("Player")
	return player
